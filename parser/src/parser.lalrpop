use clam_common::{
    ast,
    ast::{
        BinaryOperator,
    }
};
use ordered_float::OrderedFloat;
use either::Either;
use crate::lexer::LexicalError;
use crate::Token;

grammar;

Comma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Spanned<T>: ast::Span<T> = <@L> <T> <@R> => (<>);

Identifier: ast::Identifier = "identifier" => <>.into();

Primitive: ast::Primitive = {
    "int"    => ast::Primitive::Int(ast::IntType),
    "float"  => ast::Primitive::Float(ast::FloatType),
    "bool"   => ast::Primitive::Bool(ast::BoolType),
    "string" => ast::Primitive::String(ast::StringType),
    "cmd"    => ast::Primitive::Command(ast::CommandType),
}

Type: ast::Type = {
    Primitive  => ast::Type::Primitive(<>),
    Identifier => ast::Type::Name(<>),
}


Param: (ast::Span<ast::Identifier>, Option<ast::Span<ast::Type>>) = 
    <name:Spanned<Identifier>> ":"? <tname:Spanned<Type>?> => (<>);

RetType: ast::Span<ast::Type> = "->" <Spanned<Type>> => <>;

FnDef: ast::FnDef = {
    "fn" <Spanned<Identifier>>  "(" <Comma<Param>> ")" 
    <RetType?> <Spanned<Expr>>
        => ast::FnDef::new(<>)
}

StructDef: ast::StructDef = "struct" <Spanned<Identifier>> "{" <Comma<Param>> "}"
        => ast::StructDef::new(<>);

LetType: ast::Span<ast::Type> = {
    ":" <Spanned<Type>> => <>
}

LetAssign: ast::Span<Box<ast::Expr>> = "=" <Spanned<Expr>> => <>;
Let: ast::Let = "let" <Spanned<Identifier>> <LetType?> <LetAssign?>
        => ast::Let::new(<>);

Assign: ast::Assign = <Spanned<Identifier>> "=" <Spanned<Expr>>
    => ast::Assign::new(<>);

pub Statement: ast::Statement = {
    <Let> => ast::Statement::Let(<>),
    <Assign> => ast::Statement::Assign(<>),
    <FnDef> => ast::Statement::FnDef(<>),
    "break" => ast::Statement::Break,
    "return" <Spanned<Expr>?> => ast::Statement::Return(<>),
    <Spanned<Expr>> => ast::Statement::Expr(<>),
}

StatementSemi: ast::Statement = <Statement> ";";

Block: ast::Block = "{" <stmts:Spanned<StatementSemi>*> <last:Spanned<Statement>?> "}" 
    => ast::Block::new(stmts, last);


WhileLoop: ast::WhileLoop = "while" <Spanned<Expr>> <Block>
    => ast::WhileLoop::new(<>);


ElseClause: ast::Block = "else" <Block>;
Conditional: ast::Conditional = "if" <Spanned<Expr>> <Block> <ElseClause?>
    => ast::Conditional::new(<>);


StructLiteralField: (ast::Span<ast::Identifier>, ast::Span<Box<ast::Expr>>) = 
    <Spanned<Identifier>> ":" <Spanned<Expr>> => (<>);
StructLiteral: ast::Literal = "." <Spanned<Identifier>> "{"
    <Comma<StructLiteralField>> "}" => ast::Literal::Struct(<>);

Literal: ast::Literal = {
    "intLit" => ast::Literal::Int(<>),
    "floatLit" => ast::Literal::Float(<>),
    "boolLit" => ast::Literal::Bool(<>),
    "stringLit" => ast::Literal::String(<>),
    "commandLit" => ast::Literal::Command(<>),
}

Expr: Box<ast::Expr> = {
    #[precedence(level="1")]
    <Term> => <>,

    <UnaryOperator> <Spanned<Expr>> => {
        Box::new(ast::Expr::UnOp(ast::UnOp::new(<>)))
    },

    #[precedence(level="2")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecTwoOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },

    #[precedence(level="3")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecThreeOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },
    #[precedence(level="4")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecFourOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },
    #[precedence(level="5")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecFiveOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },
    #[precedence(level="6")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecSixOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },

    #[precedence(level="7")] #[assoc(side="left")]
    <lhs:Spanned<Expr>> <op:PrecSevenOp> <rhs:Spanned<Expr>> => {
        Box::new(ast::Expr::BinOp(ast::BinOp::new(op, lhs, rhs)))
    },


    // lambda def
    #[precedence(level="8")]
    // "|" <Comma<Param>> "|" <Expr> => Box::new(ast::Expr::LambdaDef(<>)),
    "bslash" <Comma<Param>> "=>" <Spanned<Expr>> => {
        Box::new(ast::Expr::LambdaDef(ast::LambdaDef::new(<>)))
    },

    // struct def
    #[precedence(level="9")] #[assoc(side="right")]
    StructLiteral => Box::new(ast::Expr::Literal(<>)),
}

pub ExternalExpr = Expr;


Term: Box<ast::Expr> = {
    Block => Box::new(ast::Expr::Block(<>)),
    Literal => Box::new(ast::Expr::Literal(<>)),
    Conditional => Box::new(ast::Expr::Conditional(<>)),
    WhileLoop => Box::new(ast::Expr::WhileLoop(<>)),
    Identifier => Box::new(ast::Expr::Identifier(<>)),
    // function call
    <Spanned<Identifier>> "(" <Comma<Spanned<Expr>>> ")" 
        => Box::new(ast::Expr::FnCall(ast::FnCall::new(<>))),
    "(" <e:Expr> ")" => e,
}

UnaryOperator: ast::UnaryOperator = {
    "!" => ast::UnaryOperator::Not,
    // TODO: add bit invert
    // "~" => ast::UnaryOperator::BitInvert,
    "-" => ast::UnaryOperator::Negate,
}

PrecTwoOp: BinaryOperator = {
    "*"  => BinaryOperator::Times,
    "/"  => BinaryOperator::Div,
    "%"  => BinaryOperator::Mod,
}

PrecThreeOp: BinaryOperator = {
    "+"  => BinaryOperator::Plus,
    "-"  => BinaryOperator::Minus,
}

PrecFourOp: BinaryOperator = {
    "<"  => BinaryOperator::Lt,
    "<=" => BinaryOperator::Lte,
    ">"  => BinaryOperator::Gt,
    ">=" => BinaryOperator::Gte,
}

PrecFiveOp: BinaryOperator = {
    "==" => BinaryOperator::Eq,
    "!=" => BinaryOperator::Ne,
}

PrecSixOp: BinaryOperator = {
    "&"  => BinaryOperator::BitAnd,
    "|"  => BinaryOperator::BitOr,
    "^"  => BinaryOperator::BitXor,
}

PrecSevenOp: BinaryOperator = {
    "&&" => BinaryOperator::And,
    "||" => BinaryOperator::Or,
}

TopLevelStmt: Either<ast::FnDef, ast::StructDef> = {
    FnDef => Either::Left(<>),
    StructDef => Either::Right(<>),
}

pub Module: ast::Mod = TopLevelStmt+
    => ast::Mod(<>);

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        "fn" => Token::Fn,
        "struct" => Token::Struct,
        "if" => Token::If,
        "else" => Token::Else,
        "for" => Token::For,
        "while" => Token::While,
        "break" => Token::Break,
        "continue" => Token::Continue,
        "return" => Token::Return,
        "in" => Token::In,
        "let" => Token::Let,
        "var" => Token::Var,
        "bool" => Token::Bool,
        "int" => Token::Int,
        "float" => Token::Float,
        "string" => Token::String,
        "cmd" => Token::Cmd,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Asterisk,
        "/" => Token::Slash,
        "%" => Token::Modulus,
        "<" => Token::Less,
        "<=" => Token::LessEqual,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "==" => Token::EqualEqual,
        "!=" => Token::NotEqual,
        "&&" => Token::LogicalAnd,
        "||" => Token::LogicalOr,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
        ":" => Token::Colon,
        ";" => Token::SemiColon,
        "=" => Token::Equal,
        "," => Token::Comma,
        "->" => Token::Arrow,
        "=>" => Token::ThiccArrow,
        "." => Token::Dot,
        "#" => Token::Hash,
        "&" => Token::Ampersand,
        "|" => Token::Pipe,
        "^" => Token::Caret,
        "!" => Token::ExclamationMark,
        "?" => Token::QuestionMark,
        "bslash" => Token::Bslash,

        "identifier" => Token::Identifier(<String>),
        "boolLit" => Token::BoolLit(<bool>),
        "stringLit" => Token::StrLit(<String>),
        "intLit" => Token::IntLit(<i64>),
        "floatLit" => Token::FloatLit(<OrderedFloat<f64>>),
        "commandLit" => Token::CommandLit(<String>),
        "error" => Token::Error
    }
}
